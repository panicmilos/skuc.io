package skuc.io;

import java.util.ArrayList;
import java.time.LocalDateTime;
import skuc.io.skuciocore.models.events.kjar.AggregateParam;
import skuc.io.skuciocore.models.events.device.ValueReceived;
import skuc.io.skuciocore.models.events.device.ValueAggregated;
import skuc.io.skuciocore.models.events.device.Value0005Aggregated;
import skuc.io.skuciocore.models.events.device.Value0015Aggregated;
import skuc.io.skuciocore.models.events.device.Value0030Aggregated;
import skuc.io.skuciocore.models.events.device.Value0060Aggregated;
import skuc.io.skuciocore.models.events.device.Value0240Aggregated;
import skuc.io.skuciocore.models.events.device.Value0480Aggregated;
import skuc.io.skuciocore.models.events.device.Value1440Aggregated;
import skuc.io.skuciocore.models.events.device.Aggregate;

import accumulate skuc.io.accumulates.ComplexMinAccumulate complexMin
import accumulate skuc.io.accumulates.ComplexMaxAccumulate complexMax
import accumulate skuc.io.accumulates.ComplexSumAccumulate complexSum
import accumulate skuc.io.accumulates.ComplexAverageAccumulate complexAverage
import accumulate skuc.io.accumulates.ComplexCountAccumulate complexCount


rule "Require 0005 Aggregation"
  timer (cron: 0/5 * * * * ?)
  when
    $vo: ValueReceived($deviceId: deviceId, $deviceType: deviceType, $paramName: paramName)
    not AggregateParam(resolution == 5, deviceId == $deviceId, deviceType == $deviceType, paramName == $paramName)
  then
    System.out.println("Require 0005 Aggregation");
    insert(new AggregateParam($deviceId, $deviceType, $paramName, 5));
end;

rule "Make 0005 Aggregation"
  when 
    $agrParam: AggregateParam(resolution == 5, $deviceId: deviceId, $deviceType: deviceType, $paramName: paramName)
    accumulate($valueReceived : ValueReceived(deviceId == $deviceId, deviceType == $deviceType, paramName == $paramName, $value: value) over window:time(5s),
      $min: min($value),
      $max: max($value),
      $sum: sum($value),
      $average: average($value),
      $count: count($value),
      $valueReceiveds: collectList($valueReceived)
    )
  then
    System.out.println("Make 0005 Aggregation");

    insert(new Value0005Aggregated($agrParam.getDeviceId(), $agrParam.getDeviceType(), $agrParam.getParamName(), new Aggregate(
      $min, $max, $sum, $average, $count
    )));

    delete($agrParam);
end;

rule "Require 0015 Aggregation"
  when
    Value0005Aggregated($deviceId: deviceId, $deviceType: deviceType, $paramName: paramName)
    $aggregation0005s : ArrayList() from collect(Value0005Aggregated(isProcessed == false, deviceId == $deviceId, deviceType == $deviceType, paramName == $paramName))
    eval($aggregation0005s.size() == 3)
    not AggregateParam(resolution == 15, deviceId == $deviceId, deviceType == $deviceType, paramName == $paramName)
  then
    System.out.println("Require 0015 Aggregation");
    insert(new AggregateParam($deviceId, $deviceType, $paramName, 15));
end;

rule "Make 0015 Aggregation"
  when 
    $agrParam: AggregateParam(resolution == 15, $deviceId: deviceId, $deviceType: deviceType, $paramName: paramName)
    accumulate($aggregation0005 : Value0005Aggregated($this: this, isProcessed == false, deviceId == $deviceId, deviceType == $deviceType, paramName == $paramName) over window:time(15s),
      $min: complexMin($this),
      $max: complexMax($this),
      $sum: complexSum($this),
      $average: complexAverage($this),
      $count: complexCount($this),
      $aggregation0005s: collectList($aggregation0005)
    )
  then

    insert(new Value0015Aggregated($agrParam.getDeviceId(), $agrParam.getDeviceType(), $agrParam.getParamName(), new Aggregate(
      $min, $max, $sum, $average, $count
    )));

    for(Object aggregation0005 : $aggregation0005s) {
      modify(((ValueAggregated)aggregation0005)) {setIsProcessed(true) };
    }

    delete($agrParam);
end;